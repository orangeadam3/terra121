buildscript {
    repositories {
        jcenter()
        maven {
            name = "Minecraft Forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:4.0.0"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: "com.github.johnrengelman.shadow"

version = "0.1"
group = "io.github.terra121" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "terra121"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "stable_39"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven {
        name = "Sonatype"
        url = "https://oss.sonatype.org/content/repositories/public"
    }
    maven { //contains Mixin, used by both Cubic Chunks and CubicWorldGen
        name = "SpongePowered"
        url = "http://repo.spongepowered.org/maven/"
    }
    maven { //contains Jankson, used by CubicWorldGen
        name = "elytradev"
        url = "https://repo.elytradev.com"
    }
}

dependencies {
    deobfProvided "io.github.opencubicchunks:cubicchunks:1.12.2-0.0-SNAPSHOT"
    deobfProvided "io.github.opencubicchunks:cubicworldgen:1.12.2-0.0-SNAPSHOT"
	shade "org.apache.commons:commons-imaging:1.0-alpha1"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    classifier = null
    configurations = [ project.configurations.shade ]
}
build.dependsOn shadowJar

reobf { //reobfuscate the shaded JAR
    shadowJar {}
}

//relocate all shaded dependencies
task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "io.github.terra121.dep"
}
shadowJar.dependsOn relocateShadowJar

